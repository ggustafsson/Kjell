#!/usr/bin/env zsh

# Description: Frontend for https://cheat.sh. Result is displayed in Vim/Nvim
# pager with custom cheat filetype for highlights.
#
# Optional: https://github.com/ggustafsson/Kjell/blob/master/src/view
#
# ~/.config/nvim/syntax/cheat.vim:
#
#   syntax match Comment /^#.*/
#
# Usage: cht
#        cht <query>
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

url=https://cheat.sh
pager="view -c 'setlocal filetype=cheat nolist' -"

if [[ $# -ge 1 ]]; then
  query=${*// /+} # Replace spaces with "+" for valid URL.
  url_full="${url}/${query}?T" # "?T" disables colors in response.

  # Fetch cheat page and fix some formatting issues.
  # Remove empty lines at start and end: /./,$!d
  # Append two empty lines before each header: s/#\[X/\n\n## \[X/g
  # Replace multiple empty lines with one: /^$/N;/^\n$/D
  response=$(curl --no-progress-meter $url_full | \
    sed -e '/^$/N;/^\n$/D' \
        -e 's/#\[cheat/\n\n## \[cheat/g' \
        -e 's/#\[tldr/\n\n## \[tldr/g' | \
    sed -e '/./,$!d')

  header="# Source: $url_full\n"
  output="${header}\n$response"
  echo $output | eval $pager
else
  curl --no-progress-meter "${url}/:help"
fi
