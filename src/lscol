#!/usr/bin/env zsh

# Description: Pretty prints "LS_COLORS" setting.
#
# Usage: lscol
#
# Original: https://unix.stackexchange.com/a/20742/13456
#
# Author: GÃ¶ran Gustafsson <gustafsson.g@gmail.com>
# License: BSD 3-Clause

if [[ -z $LS_COLORS ]]; then
  echo "Variable 'LS_COLORS' is not set! Exiting..."
  exit 1
fi

# Items and order are based on output from "dircolors" version 9.0.
typeset -A names
names[bd]="block device"
names[ca]="capability"
names[cd]="character device"
names[di]="directory"
names[do]="door"
names[ex]="executable"
names[fi]="normal file"
names[ln]="symbolic link"
names[mh]="multiple hard links"
names[mi]="missing file"
names[no]="global default"
names[or]="orphan symlink"
names[ow]="other writable"
names[pi]="named pipe"
names[rs]="reset"
names[sg]="set gid"
names[so]="socket"
names[st]="sticky"
names[su]="set uid"
names[tw]="sticky other writable"

for item in ${(s.:.)LS_COLORS}; do
  key=${item%\=*}
  color=${item#*\=}
  name=${names[$key]}
  printf '%s  \e[%sm%s\e[0m\n' $key $color $name
done
