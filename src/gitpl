#!/usr/bin/env zsh

# Description: Run git pull on all Git repos under default projects path, or
# path specified through argument.
#
# Usage: gitpl
#        gitpl <path>
#
# Author: GÃ¶ran Gustafsson (gustafsson.g at gmail.com)
# License: BSD 3-Clause

if [[ $# -eq 0 ]]; then
  projects_dir=~/Projects
elif [[ $# -eq 1 ]]; then
  projects_dir=$1
else
  echo "Usage: ${0:t} [PATH]..."
  exit
fi

if [[ ! -d $projects_dir ]]; then
  echo "Directory '$projects_dir' not found! Exiting..."
  exit 1
fi

echo "Updating all Git repos under '${projects_dir/$HOME/~}'..."

for dir in $projects_dir/*(N); do
  if ! git -C $dir rev-parse --is-inside-work-tree &> /dev/null; then
    continue
  else
    found_repo=1
  fi

  print -P "\n%B%F{cyan}${dir:t}:%f%b"
  git -C $dir pull
done

if [[ $found_repo -ne 1 ]]; then
  echo "No Git repos found."
fi
