#!/usr/bin/env zsh

# Description: Quickly run "cp file{,.bck}" on one or more files. Optional
# non-interactive mode for use as nnn plugin.
#
# ~/.zshrc:
#
#   export NNN_PLUG='b:!bck --nnn*'
#
# Usage: bck <file> ...
#        bck --nnn
#
# Author: GÃ¶ran Gustafsson <gustafsson.g@gmail.com>
# License: BSD 3-Clause

if [[ $# -lt 1 ]]; then
  echo "Usage: ${0:t} [OPTION]... [PATH(S)]...

  -n, --nnn  Run as non-interactive nnn plugin."
  exit 1
fi

function mkbck {
  if [[ $mode == nnn ]]; then
    flags=-n # Do not overwrite an existing file.
  else
    flags=-vi
  fi

  for arg; do
    if [[ -f $arg ]]; then
      cp $flags -- "$arg" "$arg.bck"
    fi
  done
}

if [[ $1 == (-n|--nnn) ]]; then
  mode=nnn
  selection=${NNN_SEL:-${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.selection}
  if [[ -s $selection ]]; then
    IFS=$'\n'
    list=($(tr '\0' '\n' < $selection))
    mkbck $list
  elif [[ -s $nnn ]]; then
    mkbck $nnn
  fi
else
  mkbck $*
fi
